#==================================================#
## PATH Configuration (consolidated, no duplicates)
#==================================================#
export MYDOTFILES="$HOME/.dotfiles"

# Build PATH array (order matters: first = highest priority)
typeset -U PATH  # -U ensures unique entries only
path=(
    "$HOME/.local/bin"
    "$HOME/bin"
    "$HOME/.bun/bin"
    "$HOME/.cargo/bin"
    "$MYDOTFILES/tmux"
    "$HOME/.mbltmon/build/src/mbltmon"
    /usr/local/bin
    /usr/local/cuda/bin
    /usr/bin
    /bin
    $path
)
export PATH

#==================================================#
## Environment
#==================================================#
export SHELL="$(which zsh)"
export ZSH="$HOME/.oh-my-zsh"
export BUN_INSTALL="$HOME/.bun"

# Locale
export LANGUAGE=en_US.UTF-8
export LC_CTYPE=en_US.UTF-8
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# export LANG
# export FZF_DEFAULT_COMMAND='fd - type f'
export FZF_DEFAULT_COMMAND='fd --type f --strip-cwd-prefix --hidden --follow --exclude .git'

#==================================================#
# terminal settings
export TERM='xterm-256color' # terminal color
export COLORTERM='truecolor'


#==================================================#
### zsh settings
export LS_COLORS="$(<$MYDOTFILES/assets/LS_COLORS)"

# OH-MY-ZSH
# themes: https://github.com/robbyrussell/oh-my-zsh/wiki/Themes
ZSH_THEME="mrtazz_custom"   # set zsh theme
DISABLE_AUTO_UPDATE="true"  # no automatically update oh-my-zsh
DISABLE_COMPFIX="true"      # skip compaudit checks (faster startup)
HIST_STAMPS="mm/dd/yyyy"    # history with date stamps


source $ZSH/oh-my-zsh.sh
setopt nosharehistory # do not share command line history across tmux windows/panes

# https://stackoverflow.com/questions/20512957/zsh-new-line-prompt-after-each-command
function precmd() {
    # Print a newline before the prompt, unless it's the
    # first prompt in the process.
    if [ -z "$NEW_LINE_BEFORE_PROMPT" ]; then
        NEW_LINE_BEFORE_PROMPT=1
    elif [ "$NEW_LINE_BEFORE_PROMPT" -eq 1 ]; then
        echo ""
    fi
}

#==================================================#
### zsh plugins
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
if [[ ! -f "${ZINIT_HOME}/zinit.zsh" ]]; then
    mkdir -p "$(dirname "$ZINIT_HOME")"
    git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi
source "${ZINIT_HOME}/zinit.zsh"

# Synchronous (lightweight)
zinit light djui/alias-tips

# Turbo mode: deferred async loading (non-blocking)
zinit ice wait lucid atload"_zsh_autosuggest_start"
zinit light zsh-users/zsh-autosuggestions
zinit ice wait lucid
zinit light zsh-users/zsh-syntax-highlighting
# fzf and fd are already installed as system binaries â€” no plugin needed


#==================================================#
### misc

# preferred editor for local and remote sessions
if [[ -n $SSH_CONNECTION ]]; then
  export EDITOR='vim'
else
  export EDITOR='vim'
fi

# Compilation flags
export ARCHFLAGS="-arch x86_64"

# ssh
export SSH_KEY_PATH="~/.ssh/id_rsa"

# personal aliases (zsh.d)
for alias_file in "$HOME/.zsh.d"/*.zsh
do
    source $alias_file
done

# fzf
function fuzzyvim()
{
    vim $(fzf)
}
zle -N fuzzyvim 

# PATH duplicates handled by typeset -U at top of file

bindkey "^[[1;3C" forward-word
bindkey "^[[1;3D" backward-word

# fash compilation
export CC='ccache gcc'
export CXX='ccache g++'

# Zoxide initialization
if [[ -o INTERACTIVE ]]; then
    eval "$(zoxide init zsh)"
fi

# Generated for envman. Do not edit.
[ -s "$HOME/.config/envman/load.sh" ] && source "$HOME/.config/envman/load.sh"

# bun completions
[ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"

#==================================================#
### zshrc auto-reload (detect changes automatically)
#==================================================#
# Monitors zshrc, theme, and zsh.d modules for changes.
# Automatically reloads on modification (checked at each prompt).
if [[ -z "$ZSHRC_AUTORELOAD_INITIALIZED" ]]; then
    export ZSHRC_AUTORELOAD_INITIALIZED=1

    # Get max mtime from all monitored files
    _zshrc_get_max_mtime() {
        local max_mtime=0
        local mtime
        local files=(
            ~/.zshrc
            $MYDOTFILES/assets/*.zsh-theme
            ~/.zsh.d/*.zsh
        )
        for f in $files; do
            [[ -f "$f" ]] || continue
            mtime=$(stat -c %Y "$f" 2>/dev/null || stat -f %m "$f" 2>/dev/null)
            (( mtime > max_mtime )) && max_mtime=$mtime
        done
        echo $max_mtime
    }

    export ZSHRC_LAST_MTIME=$(_zshrc_get_max_mtime)

    _zshrc_auto_reload() {
        # Only check every 10th prompt to reduce stat overhead
        (( ++_zshrc_reload_count % 10 == 0 )) || return
        local current_mtime=$(_zshrc_get_max_mtime)
        if (( current_mtime > ZSHRC_LAST_MTIME )); then
            echo "\033[0;36m[zshrc]\033[0m Auto-reloading..."
            exec zsh
        fi
    }

    autoload -Uz add-zsh-hook
    add-zsh-hook precmd _zshrc_auto_reload
fi
